import uk.gov.hmrc.components.buildsrc.Dependencies.Libs
import uk.gov.hmrc.components.buildsrc.Dependencies.Versions

plugins {
    id 'maven-publish'
    id 'com.gladed.androidgitversion' version '0.4.9'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'uk.gov.hmrc.spotless'
apply plugin: 'io.gitlab.arturbosch.detekt'

detekt {
    config = files("detekt/config.yml")
    baseline = file("detekt/baseline.xml")
}

version = androidGitVersion.name()
description = 'Build applications using components with the HMRC look and feel.'

android {
    viewBinding {
        enabled = true
    }
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    lint {
        abortOnError true
        baseline file('lint/baseline.xml')
        lintConfig file('lint/config.xml')
        warningsAsErrors true
    }
    namespace 'uk.gov.hmrc.components'
}


dependencies {
    implementation Libs.material
    implementation Libs.androidX_annotation
    implementation Libs.androidX_constraintlayout
    implementation Libs.androidX_core_ktx

    testImplementation Libs.junit
}


repositories {
    mavenCentral()
}

apply from: '../publishing.gradle'
