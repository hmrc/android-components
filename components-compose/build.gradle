import uk.gov.hmrc.components.buildsrc.ComposeDependencies.Libs
import uk.gov.hmrc.components.buildsrc.ComposeDependencies.Versions

plugins {
    id 'maven-publish'
    id 'com.gladed.androidgitversion' version '0.4.14'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'uk.gov.hmrc.spotless'
apply plugin: 'io.gitlab.arturbosch.detekt'

detekt {
    config = files("detekt/config.yml")
    buildUponDefaultConfig = true
}
androidGitVersion {
    format '%tag%'
    prefix 'compose/'
}

version = androidGitVersion.name()
description = 'Build applications using compose components with the HMRC look and feel.'

android {
    compileSdk Versions.compileSdk

    defaultConfig {
        minSdk Versions.minSdk
        targetSdk Versions.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildFeatures {
        compose true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }

    composeOptions {
        kotlinCompilerExtensionVersion Versions.kotlinCompilerExtensionVersion
    }
    namespace 'uk.gov.hmrc.components.compose'
}

dependencies {
    implementation platform(Libs.compose_bom)
    androidTestImplementation platform(Libs.compose_bom)

    implementation Libs.androidX_coreKtx
    implementation Libs.compose_ui
    implementation Libs.compose_tooling
    implementation Libs.compose_foundation
    implementation Libs.compose_material
    implementation Libs.compose_material_android

    testImplementation Libs.junit

    //Navigation
    implementation Libs.androidX_navigation_navigationCompose
    implementation Libs.androidX_navigation_ui
    implementation Libs.androidX_navigation_fragment

    // UI Tests
    androidTestImplementation Libs.compose_uiTest
    androidTestImplementation Libs.androidX_test_ext_junit
    androidTestImplementation Libs.androidX_test_espressoCore
}

repositories {
    mavenCentral()
}

apply from: '../publishing.gradle'
